Metadata {
  Metadata
    = Prompt newline+ NegativePrompt newline+ ModelParams

  Prompt
    = TagList

  NegativePrompt
    = negativePromptPrefix TagList
  negativePromptPrefix
    = "Negative prompt:"

  ModelParams
    = ListOf<ModelParam, tagDelimiter+>
  ModelParam
  	// Careful to only include capitalized sentence
    = capitalizedSentence ":" sentence
  capitalizedSentence
    = &upper sentence

  TagList
    // Careful not to mistaken a model param as regular tag
    // Careful not to mistaken the "Negative prompt:" prefix as regular tag
    = ListOf<(EmphasizedTag | ScheduledTag | loraTag | ~(negativePromptPrefix|ModelParam) tag), tagDelimiter+>

  EmphasizedTag
    = "(" TagList ")"  -- round
    | "(" TagList ":" number ")"  -- roundWithColon
    | "[" TagList "]"  -- square
  ScheduledTag
    = "[" sentence ":" &(sentence ":")? number "]"
  loraTag
    = "<" "lora" ":" sentence ":" number ">"
  tag
    = sentence
  sentence
    = listOf<word, space+>
  word
    = (alnum | "@" | "." | "!" | "#" | "$" | "%" | "^" | "&" | "*" | "-" | "_" | "=" | "+" | "/" | "?" | "~" | "`" | ";" | escapedChar | unicode)+
  unicode
    = "\\" "u" hexDigit hexDigit hexDigit hexDigit
  escapedChar
    = "\\" ("," | "(" | ")" | "[" | "]" | ":")
  tagDelimiter
    = "," | newline
  newline
  	= "\n" | "\r\n"
  number
    = "-"? digit* "." digit+  -- float
    | "-"? digit+  -- int
  // Redefining space rule: see https://github.com/ohmjs/ohm/discussions/297
  space := ~"\n" "\x00".."\x20"
}